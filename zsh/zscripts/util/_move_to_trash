#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# Move files / directories to the Trash.                                       #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : BUFFER, CURSOR                                                     #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local FZF_STATE_DIR="$(mktemp -d "${VI_STATE_DIR}/fzf_state_XXXXXXXXX")"
mkdir -p "$FZF_STATE_DIR"
trap 'rm -rf "$FZF_STATE_DIR"' EXIT

local HEADER_TEMPLATE="Ó™Å Trash selection.
Alt _ : Show dotfiles.
Alt ' : Expand dir tree."

local TOGGLE_HIDDEN_FLAG='
    [[ -e "'"$FZF_STATE_DIR"'/hidden" ]] \
        && rm -f "'"$FZF_STATE_DIR"'/hidden" \
        || touch "'"$FZF_STATE_DIR"'/hidden"
'

local TOGGLE_CWD_FLAG='
    [[ -e "'"$FZF_STATE_DIR"'/cwd" ]] \
        && rm -f "'"$FZF_STATE_DIR"'/cwd" \
        || touch "'"$FZF_STATE_DIR"'/cwd"
'

local RELOAD_OPTS='
    local FD_CMD="fd --no-ignore --max-depth=1 --color=always"
    local HEADER="'"$HEADER_TEMPLATE"'"

    [[ -e "'"$FZF_STATE_DIR"'/hidden" ]] && \
        HEADER="${HEADER/Show dotfiles./Hide dotfiles.}" \
        FD_CMD+=" --hidden"

    [[ -e "'"$FZF_STATE_DIR"'/cwd" ]] && \
        HEADER="${HEADER/Expand dir tree./Collapse dir tree.}" \
        FD_CMD="${FD_CMD/ --max-depth=1}"

    echo "change-header('\$HEADER')+reload('\$FD_CMD')"
'

local selection=$( \
    fd \
        --no-ignore \
        --max-depth=1 \
        --color=always \
    | fzf \
        --multi \
        --header-border=top \
        --header=$HEADER_TEMPLATE \
        --bind="alt-_:transform:$TOGGLE_HIDDEN_FLAG+$RELOAD_OPTS" \
        --bind="alt-':transform:$TOGGLE_CWD_FLAG+$RELOAD_OPTS" \
)

# Cleanup fzf state directory on exit
[[ -d "$FZF_STATE_DIR" ]] && rm -rf "$FZF_STATE_DIR"

if [[ -n "$selection" ]]; then
    # Wrap each selected line with double quotes and join them with a space
    local target
    target="$(echo "$selection" | sed 's/^/"/; s/$/"/' | paste -sd ' ')"
    BUFFER="mv -iv $target ~/.Trash/"
    CURSOR=${#BUFFER}
    zle redisplay
fi
