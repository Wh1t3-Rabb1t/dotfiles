#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# Cd into a child dir of the cwd.                                              #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : BUFFER                                                             #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local FZF_STATE_DIR="$(mktemp -d "${VI_STATE_DIR}/fzf_state_XXXXXXXXX")"
mkdir -p "$FZF_STATE_DIR"
trap 'rm -rf "$FZF_STATE_DIR"' EXIT

local HEADER_TEMPLATE="ï„• Cd into directory.
Alt _ : Show dotfiles.
Alt ' : Expand dir tree."

local TOGGLE_HIDDEN_FLAG='
    [[ -L "'"$FZF_STATE_DIR"'/hidden" ]] \
        && rm "'"$FZF_STATE_DIR"'/hidden" \
        || ln -s "" "'"$FZF_STATE_DIR"'/hidden"
'

local TOGGLE_CWD_FLAG='
    [[ -L "'"$FZF_STATE_DIR"'/cwd" ]] \
        && rm "'"$FZF_STATE_DIR"'/cwd" \
        || ln -s "" "'"$FZF_STATE_DIR"'/cwd"
'

local RELOAD_OPTS='
    local FD_CMD="fd --type=directory --max-depth=1 --color=always"
    local HEADER="'"$HEADER_TEMPLATE"'"

    [[ -L "'"$FZF_STATE_DIR"'/hidden" ]] && \
        HEADER="${HEADER/Show dotfiles./Hide dotfiles.}" \
        FD_CMD+=" --hidden"

    [[ -L "'"$FZF_STATE_DIR"'/cwd" ]] && \
        HEADER="${HEADER/Expand dir tree./Collapse dir tree.}" \
        FD_CMD="${FD_CMD/ --max-depth=1}"

    echo "change-header('\$HEADER')+reload('\$FD_CMD')"
'

local selection=$( \
    fd \
        --type=directory \
        --max-depth=1 \
        --color=always \
    | fzf \
        --header-border=top \
        --header=$HEADER_TEMPLATE \
        --bind="alt-_:transform:$TOGGLE_HIDDEN_FLAG+$RELOAD_OPTS" \
        --bind="alt-':transform:$TOGGLE_CWD_FLAG+$RELOAD_OPTS" \
)

# Cleanup fzf state directory on exit
[[ -d "$FZF_STATE_DIR" ]] && rm -rf "$FZF_STATE_DIR"

if [[ "$selection" ]]; then
    BUFFER="cd ${selection:q}"
    zle accept-line
fi
