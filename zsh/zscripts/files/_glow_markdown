#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# Render markdown files with Glow.                                             #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : COLUMNS                                                            #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local FZF_STATE_DIR="$(mktemp -d "${VI_STATE_DIR}/fzf_state_XXXXXXXXX")"
mkdir -p "$FZF_STATE_DIR"
trap 'rm -rf "$FZF_STATE_DIR"' EXIT

local HEADER_TEMPLATE="ï’Š Render markdown with Glow.
Alt _ : Show dotfiles."

local TOGGLE_HIDDEN_FLAG='
    [[ -e "'"$FZF_STATE_DIR"'/hidden" ]] \
        && rm -f "'"$FZF_STATE_DIR"'/hidden" \
        || touch "'"$FZF_STATE_DIR"'/hidden"
'

local RELOAD_OPTS='
    local FD_CMD="fd --type=file --extension=md --color=always"
    local HEADER="'"$HEADER_TEMPLATE"'"

    [[ -e "'"$FZF_STATE_DIR"'/hidden" ]] && \
        HEADER="${HEADER/Show dotfiles./Hide dotfiles.}" \
        FD_CMD+=" --hidden"

    echo "change-header('\$HEADER')+reload('\$FD_CMD')"
'

local selection=$( \
    fd \
        --type=file \
        --extension=md \
        --color=always \
    | fzf \
        --header-border=top \
        --header=$HEADER_TEMPLATE \
        --bind="alt-_:transform:$TOGGLE_HIDDEN_FLAG+$RELOAD_OPTS" \
)

# Cleanup fzf state directory on exit
[[ -d "$FZF_STATE_DIR" ]] && rm -rf "$FZF_STATE_DIR"

# Glow markdown
[[ -n "$selection" ]] && glow --width $(( COLUMNS - 5 )) "$selection"
