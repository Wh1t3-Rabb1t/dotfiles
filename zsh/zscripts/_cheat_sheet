#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# Terminal cheat sheet powered by fzf.                                         #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : BUFFER, CURSOR, ZSCRIPTDIR                                         #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local selection
local initial_dir="${0:A:h}"

cd "${ZSCRIPTDIR}"

# Search cheat sheet, ignoring files with a leading underscore
if (( ${+commands[fd]} )); then
    selection=$( \
        fd \
            --type f \
            --exclude '_*' \
            --exclude 'README.md' \
            --color always \
        | fzf \
    )
elif (( ${+commands[find]} )); then
    selection=$( \
        find . \
            -type f \
            ! -name '_*' \
        | fzf \
    )
fi

cd "$initial_dir"

if [[ "$selection" ]]; then
    BUFFER="${selection#*/}"  # Strip everything up to, and including the slash
    CURSOR=${#BUFFER}         # Move the cursor to the end of the appended command
    zle redisplay
fi
