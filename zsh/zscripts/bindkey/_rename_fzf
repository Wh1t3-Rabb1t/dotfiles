#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# Fzf cwd and append `mv` and the rename target (x2) to the command line.      #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : BUFFER                                                             #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local selection

# Use `eza` over `gls` if available
if (( ${+commands[eza]} )); then
    selection=$( \
        eza \
            --almost-all \
            --icons=always \
            --color=always \
        | fzf \
            --header=' Rename selected file / directory.' \
            --no-preview \
    )
elif (( ${+commands[gls]} )); then
    selection=$( \
        gls \
            --almost-all \
            --group-directories-first \
            --color \
        | fzf \
            --header=' Rename selected file / directory.' \
            --no-preview \
    )
fi

if [[ "$selection" ]]; then
    local quotes=\'

    # Remove icons if `eza` was used
    (( ${+commands[eza]} )) && selection="$(echo "$selection" | sed -E 's/^.{2}//')"

    # Use double quotes if a single quote exists in the selection
    [[ $selection == *\'* ]] && quotes=\"

    # Add quotes and update BUFFER
    selection=$(echo "$selection" | sed -E "s/(.*)/$quotes\1$quotes/")
    BUFFER="mv $selection $selection"

    # Navigate to the start of the second name
    zle -U "$quotes"
    zle vi-cmd-mode
    zle end-of-line
    zle vi-backward-char
    zle vi-find-prev-char-skip
fi
