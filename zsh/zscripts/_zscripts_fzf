#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# Terminal cheat sheet powered by fzf.                                         #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : BUFFER, CURSOR, ZSCRIPTDIR                                         #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local selection

pushd "$ZSCRIPTDIR"

# Search cheat sheet, ignoring files with a leading underscore
if (( ${+commands[fd]} )); then
    selection=$( \
        fd \
            --type=file \
            --exclude='_*' \
            --color=always \
        | fzf --header=' Zsh cheat sheet.' \
    )
elif (( ${+commands[find]} )); then
    selection=$( \
        find . \
            -type f \
            ! -name '_*' \
        | fzf --header=' Zsh cheat sheet.' \
    )
fi

popd

if [[ "$selection" ]]; then
    # Strip everything up to, and including the slash
    BUFFER="${selection#*/}"

    # Execute marked commands on selection
    if [[ "${BUFFER: -2}" == "_x" ]]; then
        zle accept-line
    else
        # Move the cursor to the end of the appended command
        CURSOR=${#BUFFER}
        zle redisplay
    fi
fi
