#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# Search for Vim sessions, cd to parent dir, and open selected session.        #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : BUFFER                                                             #
# ---------------------------------------------------------------------------- #

# TODO: use a coproc to read from the subshell

# If a pipeline is preceded by `coproc', it is executed as a coprocess; a two-way
# pipe is established between it and the parent shell.  The shell can read from
# or write to the coprocess by means of the `>&p' and `<&p' redirection operators
# or with `print -p' and `read -p'.  A pipeline cannot be preceded by both
# `coproc' and `!'.  If job control is active, the coprocess can be treated in
# other than input and output as an ordinary background job.

emulate -L zsh

local vim_sessions="${VI_STATE_DIR}/vim_sessions"
local vim_session_swap="${VI_STATE_DIR}/vim_session_swap"
[[ ! -f "$vim_sessions" ]] && touch "$vim_sessions"
[[ ! -f "$vim_session_swap" ]] && touch "$vim_session_swap"

(
    local verify_sessions=$(
        fd Session.vim \
            --extension=.vim \
            --search-path=$HOME \
            --exclude=.cache \
            --exclude=Library \
            --fixed-strings \
            --hidden \
            --no-ignore \
            --absolute-path \
            --format={//} \
    )

    if [[ -n "$verify_sessions" ]]; then
        # Ensure results are sorted
        echo "$verify_sessions" | sort > "$vim_session_swap"

        if ! cmp -s "$vim_session_swap" "$vim_sessions"; then
            cp "$vim_session_swap" "$vim_sessions"
        fi
    fi
) &> /dev/null &!

local HEADER="î˜« Open session.
Alt r : Reload results."

local selection=$(
    cat "$vim_sessions" \
    | fzf \
        --tac \
        --keep-right \
        --header-border=top \
        --header=$HEADER \
        --bind="alt-r:reload(cat "$vim_sessions")"
)

if [[ -n "$selection" && -e "${selection}/Session.vim" ]]; then
    BUFFER="cd ${selection:q}"
    zle accept-line
    nvim -S "${selection}/Session.vim"
fi
