#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# FZF cwd and append `mv` and the chosen rename target (x2) to the cli.        #
# ---------------------------------------------------------------------------- #
# Args    : None                                                               #
# Globals : BUFFER                                                             #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local selection

# Use `lsd` over `ls` if available
if (( ${+commands[lsd]} )); then
    selection="$(lsd -A -v | fzf --no-preview --ansi)"
elif (( ${+commands[ls]} )); then
    selection="$(ls -a | fzf --no-preview --ansi)"
fi

if [[ -n "$selection" ]]; then
    local quotes=\'

    # Remove icons if lsd was used over ls
    (( ${+commands[lsd]} )) && selection="$(echo "$selection" | sed -E 's/^.{2}//')"

    # Use double quotes if a single quote exists in the selection
    [[ $selection == *\'* ]] && quotes=\"

    # Add quotes and update BUFFER
    selection="$(echo "$selection" | sed -E "s/(.*)/$quotes\1$quotes/")"
    BUFFER="mv $selection $selection"

    # Navigate to the start of the second name
    zle -U "$quotes"
    zle vi-cmd-mode
    zle end-of-line
    zle vi-backward-char
    zle vi-find-prev-char-skip
fi
