#!/usr/bin/env zsh

# ---------------------------------------------------------------------------- #
# List permissions of all directories leading up to a filename.                #
# This is useful to see if some permissions are blocking access to a file.     #
# If no args are passed, lists entire parent dir tree over cwd.                #
# ---------------------------------------------------------------------------- #
# Args    : File name / None                                                   #
# Globals : None                                                               #
# ---------------------------------------------------------------------------- #

emulate -L zsh

local pathlist

if [[ "${1}" = "${1##/}" ]]; then
    pathlist=(/ ${(s:/:)PWD} ${(s:/:)1})
else
    pathlist=(/ ${(s:/:)1})
fi

local allpaths=()
local filepath=${pathlist[0]}

shift pathlist
for i in ${pathlist[@]}; do
    allpaths=(${allpaths[@]} ${filepath})
    filepath="${filepath%/}/$i"
done
allpaths=(${allpaths[@]} ${filepath})

# Use `eza` over `gls` if available
if (( ${+commands[eza]} )); then
    eza \
        --long \
        --list-dirs \
        --sort=inode \
        --group \
        --git \
        --git-repos \
        --header \
        --almost-all \
        --classify=auto \
        --icons=auto \
        --color=auto \
    "${allpaths[@]}"
elif (( ${+commands[gls]} )); then
    gls \
        -l \
        --directory \
        --almost-all \
        --classify \
        --human-readable \
        --color \
    "${allpaths[@]}"
fi
